plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

def dartDefines = [
        "APP_NAME"     : "Recago",
        "APP_ID_SUFFIX": ""
]
if (project.hasProperty('dart-defines')) {
    def defineItems = project.property('dart-defines').toString().split(',')
    for (defineItem in defineItems) {
        def items = new String(defineItem.decodeBase64(), 'UTF-8').split('=')

        dartDefines[items.first()] = items.last()
    }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode")
if (flutterVersionCode == null) {
    flutterVersionCode = "1"
}

def flutterVersionName = localProperties.getProperty("flutter.versionName")
if (flutterVersionName == null) {
    flutterVersionName = "1.0"
}

android {
    namespace = "com.vanvixi.dart_defines_config"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "com.vanvixi.dart_defines"
        applicationIdSuffix = dartDefines.APP_ID_SUFFIX
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
        resValue "string", "app_name", dartDefines.APP_NAME
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig = signingConfigs.debug
        }
    }

    flavorDimensions "flavor"
    productFlavors {
        local { dimension "flavor" }
        dev { dimension "flavor" }
        stg { dimension "flavor" }
        prod { dimension "flavor" }
    }
}

flutter {
    source = "../.."
}
